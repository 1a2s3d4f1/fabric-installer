apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
}

sourceCompatibility = 1.8
version = '0.1.0'
archivesBaseName = "fabric-installer"

// Fetch build number from Jenkins
def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = version + "." + "${System.getenv().BUILD_NUMBER}"
}

repositories {
    mavenCentral()
    maven {
        name = 'fabric'
        url = 'https://maven.fabricmc.net/'
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'commons-io:commons-io:2.6'
    compile 'org.apache.maven:maven-repository-metadata:3.5.4'
}

task copyAndReplace(type: Copy) {
    from(sourceSets.main.java) {
        include '**/Reference.java'
        expand 'DEV': project.version
    }
    from(sourceSets.main.java) {
        exclude '**/Reference.java'
    }
    into project.getBuildDir().toString() + "/sources/java"
}
tasks.compileJava.dependsOn copyAndReplace
tasks.compileJava.setSource(project.getBuildDir().toString() + '/sources/java')


jar {
    manifest {
        attributes 'Implementation-Title': 'FabricInstaller',
                'Implementation-Version': version,
                'Main-Class': 'net.fabricmc.installer.Main'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/license.gradle'
apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/maven.gradle'